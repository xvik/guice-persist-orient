plugins {
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'ru.vyarus.java-lib' version '1.0.1'
    id 'ru.vyarus.github-info' version '1.0.0'
    id 'ru.vyarus.animalsniffer' version '1.0.0'
    id 'ru.vyarus.quality' version '1.2.0'
    id 'com.github.kt3k.coveralls' version '2.4.0x'
    id 'com.jfrog.bintray' version '1.5'
    id 'net.researchgate.release' version '2.3.4'
    id 'com.github.ben-manes.versions' version '0.11.3'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

wrapper {
    gradleVersion = 2.10
}

ext {
    orient = '2.1.5'
    guice = '4.0'
}

repositories { jcenter(); mavenCentral(); mavenLocal() }
dependencies {
    signature 'org.codehaus.mojo.signature:java16-sun:+@signature'

    provided 'com.google.code.findbugs:jsr305:3.0.1'
    provided 'com.google.code.findbugs:annotations:3.0.1'

    compile 'ru.vyarus:generics-resolver:2.0.1'
    compile 'ru.vyarus:guice-ext-annotations:1.1.1'
    compile 'org.javassist:javassist:3.16.1-GA'
    compile "com.google.inject:guice:$guice"
    compile "com.google.inject.extensions:guice-persist:$guice"
    compile "com.google.inject.extensions:guice-multibindings:$guice"
    compile "com.orientechnologies:orientdb-core:$orient"
    compile "com.orientechnologies:orientdb-object:$orient"
    compile ("com.orientechnologies:orientdb-graphdb:$orient") {
        // excluded to remove old groovy dependency
        exclude module: 'gremlin-groovy'
    }
    provided ("com.orientechnologies:orientdb-lucene:$orient") {
        // excluded to remove old groovy dependency
        exclude module: 'gremlin-groovy'
    }

    testCompile 'ch.qos.logback:logback-classic:1.1.3'
    testCompile 'org.slf4j:jul-to-slf4j:1.7.13'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'org.spockframework:spock-guice:1.0-groovy-2.4'
    testCompile 'ru.vyarus:guice-validator:1.1.0'
    testCompile 'org.hibernate:hibernate-validator:5.1.3.Final'
    testCompile 'org.glassfish.web:javax.el:2.2.6'
    testCompile "com.orientechnologies:orientdb-tools:$orient"
}

group = 'ru.vyarus'
description = 'Guice integration for OrientDB'

github {
    user = 'xvik'
    license = 'MIT'
}

pom {
    developers {
        developer {
            id "xvik"
            name "Vyacheslav Rusakov"
            email "vyarus@gmail.com"
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : 'USER'
    key = project.hasProperty('bintrayKey') ? bintrayKey : 'KEY'
    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'xvik'
        name = project.name
        desc = project.description
        labels = ['guice', 'guice-persist', 'orientdb']
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = project.hasProperty('gpgPassphrase') ? gpgPassphrase : ''
            }
            mavenCentralSync {
                sync = true
                user = project.hasProperty('sonatypeUser') ? sonatypeUser : 'USER'
                password = project.hasProperty('sonatypePassword') ? sonatypePassword : 'PASSWORD'
            }
        }
    }
}

afterReleaseBuild {
    dependsOn = [bintrayUpload]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    testLogging {
        events "skipped", "failed", "standard_error"
        exceptionFormat "full"
    }
    maxHeapSize = "512m"
}

dependencyUpdates.revision = 'release'
jacocoTestReport.reports.xml.enabled = true

task showDependenciesTree(dependsOn: 'htmlDependencyReport', group: 'help', description:
        'Generates dependencies tree report and opens it in browser') << {
    java.awt.Desktop.getDesktop().open(file('/build/reports/project/dependencies/root.html'))
}
